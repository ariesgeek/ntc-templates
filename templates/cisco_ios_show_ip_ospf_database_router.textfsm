Value Filldown ROUTER_ID (\d+\.\d+\.\d+\.\d+)
Value Filldown PROCESS_ID (\d+)
Value Filldown AREA (\d+\.\d+\.\d+\.\d+|\d+)
Value Required LSA_AGE (\d+)
Value Required LSA_OPTIONS (.+)
Value Required LSA_TYPE (.+)
Value Required LSA_ID (\d+\.\d+\.\d+\.\d+)
Value Required LSA_ADV_ROUTER (\d+\.\d+\.\d+\.\d+)
Value Required LSA_SEQ_NUMBER (\S+)
Value Required LSA_CHECKSUM (\S+)
Value Required LSA_LENGTH (\d+)
Value LSA_ASBR (AS\sBoundary\sRouter)
Value Required LSA_NUM_LINKS (\d+)
Value List LS_LINK_TYPE (.+?)
Value List LS_LINK_ID (\d+\.\d+\.\d+\.\d+)
Value List LS_LINK_DATA (\d+\.\d+\.\d+\.\d+)
Value List LS_MTID_METRICS (\d+)
Value List LS_TOS_0_METRICS (\d+)

Start
  ^\s*OSPF\s+Router\s+with\s+ID\s+\(${ROUTER_ID}\)\s+\(Process\s+ID\s+${PROCESS_ID}\)$$
  ^\s+Router\s+Link\s+States\s+\(Area\s${AREA}\)$$ -> LSA_Header
  ^. -> Error

LSA_Header
  ^\s+LS\s+age:\s+${LSA_AGE}$$
  ^\s+Options:\s+\(${LSA_OPTIONS}\)$$
  ^\s+LS\s+Type:\s+${LSA_TYPE}$$
  ^\s+Link\s+State\s+ID:\s+${LSA_ID}$$
  ^\s+Advertising\s+Router:\s+${LSA_ADV_ROUTER}$$
  ^\s+LS\s+Seq\s+Number:\s+${LSA_SEQ_NUMBER}$$
  ^\s+Checksum:\s+${LSA_CHECKSUM}$$
  ^\s+Length:\s+${LSA_LENGTH}$$
  ^\s+${LSA_ASBR}$$
  ^\s+Number\s+of\s+Links:\s+${LSA_NUM_LINKS}$$ -> Link
  ^. -> Error

Link
  ^\s+Link\s+connected\s+to:\s+${LS_LINK_TYPE}$$
  ^\s+\(Link\sID\)\s+((Network\/subnet\s+number)|(Designated\s+Router\s+address)|(Neighboring\s+Router\sID)):\s+${LS_LINK_ID}$$
  ^\s+\(Link\sData\)\s+((Network\s+Mask)|(Router\s+Interface\s+address)):\s+${LS_LINK_DATA}$$
  ^\s+Number\s+of\s+MTID\s+metrics:\s+${LS_MTID_METRICS}$$
  ^\s+TOS\s+0\s+Metrics:\s+${LS_TOS_0_METRICS}$$
  ^\s*$$ -> Link_EmptyLine
  ^. -> Error

Link_EmptyLine
  # An empty line in the Link state indicates the end of the information for
  # that link, triggering the Link_EmptyLine state.  The contents of the next
  # command output line will trigger the LSA_Header state or the Link state.
  #
  # If the LSA contains no additional links, the next line from the command
  # output line is empty, and will trigger Record and the LSA_Header state.
  #
  # If the LSA contains additional links, the next line from the command
  # output will match the same Link state trigger as in the LSA_Header state.
  ^\s+Link\s+connected\s+to:\s+${LS_LINK_TYPE}$$ -> Link
  ^\s*$$ -> Record LSA_Header
  ^. -> Error

